# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=openssh
pkgver=7.9p1
pkgrel=3
pkgdesc='Free version of the SSH connectivity tools'
url='http://www.openssh.org/portable.html'
license=('custom:BSD')
arch=('i686' 'x86_64')
groups=('net-utils')
depends=('heimdal' 'libedit' 'libcrypt' 'openssl')
makedepends=('heimdal-devel' 'libedit-devel' 'libcrypt-devel' 'openssl-devel')
source=("https://cloudflare.cdn.openbsd.org/pub/OpenBSD/OpenSSH/portable/${pkgname}-${pkgver}.tar.gz"{,.asc}
        0001-Forward-port-MSys2-patches.patch
        0002-openssh-work-around-Cygwin-declaring-setkey.patch
        0003-openssh-skip-privilege-separation-tests.patch
        0004-Allow-scp-to-copy-files-that-start-with-a-Windows-dr.patch)
sha256sums=('6b4b3ba2253d84ed3771c8050728d597c91cfce898713beb7b64a305b6f11aad'
            'SKIP'
            'a58e15676be4f16362aa183bc142f2d3dd97b054085e9462733902da4cb3c58f'
            '24cb7db3408987a37a78299e5f057008b47803d11fa03d70005ec7efed15e8a8'
            'f45c7cd23586f186918f061e13bfcdb1c8f0f02236e3b23f28fa4f7c0c0dcf3c'
            '8c151edc01effa71afe054140af7eed54acdc504e1457556609c98f5aeaece50')
validpgpkeys=('59C2118ED206D927E667EBE3D3E5F56B6D920D30')

backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-Forward-port-MSys2-patches.patch
  patch -p1 -i ${srcdir}/0002-openssh-work-around-Cygwin-declaring-setkey.patch
  patch -p1 -i ${srcdir}/0003-openssh-skip-privilege-separation-tests.patch
  patch -p1 -i ${srcdir}/0004-Allow-scp-to-copy-files-that-start-with-a-Windows-dr.patch
  autoreconf -fvi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  TEST_SSH_UTF8=no \
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh \
    --localstatedir=/var \
    --with-libedit \
    --with-kerberos5=/usr \
    --disable-strip

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make tests ||
  grep $USER /etc/passwd | grep -q /bin/false
  # connect.sh fails when run with stupid login shell
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #ln -sf ssh.1.gz "${pkgdir}"/usr/share/man/man1/slogin.1.gz
  install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"

  install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

  #rm -f "${pkgdir}"/etc/ssh/sshd_config
  #rm -f "${pkgdir}"/usr/bin/sshd.exe
  #rm -f "${pkgdir}"/usr/sbin/sshd.exe
  #rm -f "${pkgdir}"/usr/lib/ssh/sftp-server.exe
}
