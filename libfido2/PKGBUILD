# Contributer: Reza Tavakoli <rta.0070@gmail.com>

pkgbase="libfido2"
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=1.5.0
pkgrel=1
pkgdesc="libfido2 provides library functionality and command-line tools to communicate with a FIDO device over USB, and to verify attestation and assertion signatures."
arch=('i686' 'x86_64')
url="https://github.com/Yubico/libfido2"
license=('BSD-2-Clause')
depends=("libcbor" "openssl")
makedepends=("libcbor-devel" "openssl-devel" "cmake" "make" "gcc")
source=(${pkgbase}-${pkgver}.tar.gz::"$url/archive/${pkgver}.tar.gz"
        0001-msys-fixes.patch)
sha256sums=('5990f923c9390fe1e6a00ba5d1d1f74030e7344b855e971d9fb7223e70ff3122'
            '4f439f11f599583584a55050084e768f10e7c710d2f078982adf4154aae9418b')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -p1 -i ${srcdir}/0001-msys-fixes.patch
}

build() {
  mkdir -p "${srcdir}/${pkgbase}-${pkgver}/build-${CARCH}"
  cd "${srcdir}/${pkgbase}-${pkgver}/build-${CARCH}"

  #cmake files are changing rapidly on this project, careful when upgrading
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..

  make
  make DESTDIR=${srcdir}/dest install
}

package_libfido2() {
  groups=("libraries")
  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/man/man1
  cp usr/lib/*.dll ${pkgdir}/usr/bin/
  cp usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf usr/share/man/man1/* ${pkgdir}/usr/share/man/man1
}

package_libfido2-devel() {
  pkgdesc="Development headers and library for libfido2"
  depends=("$pkgbase=$pkgver")
  groups=("development")
  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/lib
  mkdir -p ${pkgdir}/usr/include
  mkdir -p ${pkgdir}/usr/share/man/man3
  cp -rf usr/lib/*.a ${pkgdir}/usr/lib
  cp -rf usr/include/* ${pkgdir}/usr/include
  cp -rf usr/share/man/man3/* ${pkgdir}/usr/share/man/man3
}
