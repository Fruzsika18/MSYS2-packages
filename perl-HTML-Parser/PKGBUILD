# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTML-Parser
pkgname=perl-${_realname}
pkgver=3.78
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/HTML-Parser/"
groups=('perl-modules')
depends=('perl-HTML-Tagset' 'perl')
makedepends=('libcrypt-devel')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('22564002f206af94c1dd8535f02b0d9735125d9ebe89dd0ff9cd6c000e29c29d')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
