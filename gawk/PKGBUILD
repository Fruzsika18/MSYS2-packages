# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gawk
pkgver=4.2.1
pkgrel=2
pkgdesc="GNU version of awk"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gawk/"
license=('GPL')
groups=('base' 'base-devel')
depends=('sh' 'mpfr' 'libintl' 'libreadline')
makedepends=('gettext-devel' 'mpfr-devel' 'libreadline-devel')
provides=('awk')
install=gawk.install
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        0001-Support-building-in-MSYS2.patch
        0002-Revert-Update-posix-gawkmisc.c-remove-cygwin-code.patch)
sha256sums=('2b23d51503b2df9a41aa6fddc6002ad7ebf2a386ac19dc1b6be0dd48b0acf6db'
            'SKIP'
            '779f410c62df48d55aae5fbefd2ab616b8e489c4d9c462ea4065d7dacda1e71a'
            '4880e93bc5f81ab6c5579a0116c9567f067eda9099f0cce841f6cd8623b465d7')
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Support-building-in-MSYS2.patch
  patch -p1 -i ${srcdir}/0002-Revert-Update-posix-gawkmisc.c-remove-cygwin-code.patch
  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
              --prefix=/usr \
              --libexecdir=/usr/lib \
              --without-libiconv-prefix \
              --without-libintl-prefix \
              --without-libsigsegv

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
