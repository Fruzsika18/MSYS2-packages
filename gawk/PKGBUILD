# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gawk
pkgver=5.0.0
pkgrel=1
pkgdesc="GNU version of awk"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/gawk/"
license=('GPL')
groups=('base' 'base-devel')
depends=('sh' 'mpfr' 'libintl' 'libreadline')
makedepends=('gettext-devel' 'mpfr-devel' 'libreadline-devel')
provides=('awk')
install=gawk.install
source=(https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        0001-Support-building-in-MSYS2.patch)
sha256sums=('2d686c9a5b7ba292bc3fa56bc84a1959d721a9d57970a76413156d4b8f1fc67d'
            'SKIP'
            '295806ef3dd010584c2fff55f438fffea905b8b03420d23565ae41a8345b22d9')
validpgpkeys=('D1967C63788713177D861ED7DF597815937EC0D2')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-Support-building-in-MSYS2.patch
  # Prevent autopoint from overriding our patched config.rpath
  AUTOPOINT=true autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
              --prefix=/usr \
              --libexecdir=/usr/lib \
              --without-libiconv-prefix \
              --without-libintl-prefix \
              --without-libsigsegv

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
