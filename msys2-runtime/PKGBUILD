# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.0.5
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS-triplets.patch
        0002-Rename-DLL-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-convert-be-more-careful-to-stay-within-the-buffer.patch
        0010-convert-check-safely-whether-we-could-allocate-the-b.patch
        0011-convert-use-the-elegant-realloc-call.patch
        0012-convert-plug-potential-memory-leak.patch
        0013-convert-warn-when-we-cut-off-a-path.patch
        0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
        0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
        0016-Special-case-for-converting-root-directory-to-have-t.patch
        0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0019-strace.cc-Don-t-set-MSYS-noglob.patch
        0020-Add-debugging-for-build_argv.patch
        0021-Add-debugging-for-strace-make_command_line.patch
        0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0023-path.cc-Ignore-zero-length-exclusions.patch
        0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0025-Pass-environment-variables-with-empty-values.patch
        0026-Handle-8-bit-characters-under-LOCALE-C.patch
        0027-Mention-the-extremely-useful-small_printf-function.patch
        0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0033-Leave-paths-containing-any-special-characters-alone.patch
        0034-Leave-paths-containing-alone.patch
        0035-Skip-posix-to-windows-conversion-when-is-seen.patch
        0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0037-Arguments-starting-with-are-no-paths.patch
        0038-Prevent-scp-style-arguments-from-being-mangled.patch
        0039-Fixed-path-converting-with-non-ascii-char.patch
        0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0042-kill-kill-Win32-processes-more-gently.patch
        0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0047-POSIX-ify-the-SHELL-variable.patch
        0048-Make-paths-WCS-MBS-conversion-explicit.patch
        0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0050-Fix-incorrect-path-conversion-trailing-slash.patch
        0051-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0052-srandomdev-avoid-warning-about-uninitialized-use.patch)
sha256sums=('SKIP'
            '686e8cdd6b6c725afff9b125ffe8d50f96e88a03cb530bc584f353f22c6433c4'
            'a9444fa3f7c8d39872d6421809ea171946c5fa3ea9d617493fc1eb32a8ab4ca0'
            '1821c6d9b4e89f14579a9c464b313c8274d64600538a5c9b7bf94f828eb7e022'
            '1e3c951ce259ddda2fb73c9fface1e003be2ca92ce1ea749406b113e96dcd682'
            'aeddc29da747f352de52496b13b4c214ef7f6868fff92ebbf9f73443a75187f4'
            '14b85d2a05bc0618c35791a16a8e5d7426c5212d8088e08acdc2109c4a0039f3'
            'cf3ccc2749f6fe30fd7fd168220c2c872cd967338dcd1cc1dd7493111499d5fc'
            '740a6ce572aa0d0969dd1f8c38952c9b4c9f370720d6765c20ef15e602d947b5'
            '426ca4ed26dec01ee9a79300c06685ff3cde199e6e67ce3c012f0c5ad5db7b7f'
            '56efccb5509efbfd62cecc4ab1d6d9756560722cd56248abad9d84a5a747854c'
            '3a3c14bd7e13aa4a746728703b062dfcaf9e50224dc3ce1ca17365fb5b1efc2a'
            'b5c776b1bef824ed91692b321dc251bfa6e35c8e3d2c1626a34e28ea1a479750'
            '84589c6c0a121ff9e0652bc47034dc6d94c72dde37bbf036b03f14d7d0130270'
            '4d6eba02de16772fe963ddb70e96121aeefe20c3fa29db094c7e178fec2f0b69'
            '3cb51fca977f78eb47aff600315e2de0a510e933c99098573bd559e7210ff3ef'
            '6b3d84faf0501842f4593c02f14ce3efdad461f4784b6aa7fdb4a298ecf3f872'
            '203c24e6b1e0ad331212ece8fe48c79d69fffce7c067e2a89fbe910654685fd5'
            '5d6a5a3a5170ad1083b2359cded6b670dd8d8e3901d3b3c7bfe9b85ca743c449'
            '9285b45665653ee13f6997d724b7937033c2477ccb4d88e1587115ec59873631'
            '65dff90e1376af1bf3340f7ba19f60383bf2c49c699fa88cc08fdc5c2460e654'
            '6a606a34df34ba36c02afe5b64dcae0fc8e29caf54e99253dd7ded8ab829fe09'
            'ec03875bebc39ff9e09b548fe07e7025acfab7bd525b0ec2cef207919fa37fa4'
            '31c4d0272d82e7a5f788e6ce1f5173cfd17adfccdb52424d0f9cdc6e3b4ccd2e'
            'e1fe9f8d0b98e59f2fa64abe5997ad0e3de539b34e5f335bf70b1fc0bddc4b9c'
            '0c45543f56df8b44b223571b4d96db1ae79c5e573727dc59ec2896733e2b3dea'
            'e148fdbc48116dd773febe4fb7cc9c95cf6c68d307a7cb6bb37584d2fe6ffb83'
            '336db6c6ad25afcb8c0680cba3fe4d0e8663dcb7f2d3c3c0d7e80724ef73e133'
            '29feb2dd278bf9d5b57a51fcfdfa61d11c3ca234e131d845f2cc13e9246a96f5'
            'dcae91240000cac9d35ad95bcc250aa7d0301c5821cd4a07513c2b5e8befe5c3'
            '86a6b0e6a0646b435187f821b8471b440a65496bdb9921a98c4fa82e77f92e30'
            '0d58fa8f9c264bfe6d4951061fca8e05f3b0db04fdabcd23925fbbdd89c14aac'
            '43719c11b9c6b573f79a5954da5cb29076a7c7f5e71112e0338f214fbd41467a'
            'aaae384e123ff49e70826acc340379f859b7de19006d7f1584b51e4d15aa7013'
            '8fba0a452e177057536e126da7e9bdc1c1c9d19d3381e94ccb5c1bebee231616'
            '60dce39cab5c4c4967a53d67d9e7762618d5889598b45e843e6d9a72f6d202bf'
            '7930046d397f893ccc75e13742a179e541189924c6514175be783b03f66fafac'
            '1d45100255c9573cad1cfdf89fab3651a7770667ce4b204aac374d924711bd51'
            '0e181113ed669caf96551f0c32694a8fe86be5d155d31a88d75581a57c03fb6f'
            '2b29749d03fb137b5a6e7bdade1855950e50906f9da80c0f8e787b477a8bfaa7'
            '6e1e31f989c505d13a29c7ca6a7c69d758578ed4ac2d7bbef01c349f960671e1'
            '03c03c48d22223bb816327b338f23be20d8922e79249b0674d435b04eb0b97ad'
            '59f02466c2b9c993878ad2656a5ee3d32a2449be2d750bf201b97b440732ff9e'
            'b7599bac7bef72e6283434cd34ddefd545b01fe891f5eee0033de4dcec027e87'
            '586246e181c497e1baa4e9eae0ca2825f6be7cb7e0e954ba420bd5633c784d8f'
            'c1409f0c3d6d4b64fe00139968be00daa3fe10e62fd5dfe16748cf49c5770576'
            '4909a250130309db1bbcc7902420f546d0bc17f72ac6e9071c3bc89c8494a3fa'
            'f19477d741a84f6cc32c5177ce343f84e13c3b96b74fd6bd4e98c7f6b8611c15'
            '4f8df436f3a50fbba0d565f94a50061a67fbe2bbc1e54cbbd5c1e26370e30c4f'
            '92de368d678e8bb6047e568fabff6a15c9ac8cf9e0bf97b8a867481a82f4e4b7'
            'd17419ebc57c90d91a7896b64bfd18a17329ac8315f418e8b53886ac3870f788'
            'af7d8c0f0722335d6436b7bb600e68437d30ef9ca6ea2f3444ccf12e6d1a4668'
            'e793fc607f20ebc60e4804c4cc6e1ba648c054298cc1c563d7a19703c668abd1')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS-triplets.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-DLL-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-Do-no.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-convert-be-more-careful-to-stay-within-the-buffer.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-convert-check-safely-whether-we-could-allocate-the-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-convert-use-the-elegant-realloc-call.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-convert-plug-potential-memory-leak.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-convert-warn-when-we-cut-off-a-path.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-arg_heuristic-be-better-prepared-to-handle-path-list.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Fix-converting-argument-looks-like-DVAR-str1-c-path1.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-path.cc-Ignore-zero-length-exclusions.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0052-srandomdev-avoid-warning-about-uninitialized-use.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
