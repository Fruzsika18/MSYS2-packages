# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.5
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-POSIX-ify-the-SHELL-variable.patch
        0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0036-Implicitly-support-the-dev-fd-symlink-and-friends.patch
        0037-Fix-incorrect-path-conversion-trailing-slash.patch
        0038-Pass-environment-variables-with-empty-values.patch
        0039-Handle-8-bit-characters-under-LOCALE-C.patch
        0040-Mention-the-extremely-useful-small_printf-function.patch
        0041-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0042-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0043-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0044-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0045-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0046-Leave-paths-containing-any-special-characters-alone.patch
        0047-Leave-paths-containing-alone.patch
        0048-Skip-posix-to-windows-conversion-when-is-seen.patch
        0049-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0050-Arguments-starting-with-are-no-paths.patch
        0051-Prevent-scp-style-arguments-from-being-mangled.patch
        0052-Fixed-path-converting-with-non-ascii-char.patch
        0053-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0054-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0055-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0056-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0057-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0058-Make-paths-WCS-MBS-conversion-explicit.patch
        0059-Use-MB_CUR_MAX-6-by-default.patch
        0060-Change-the-default-base-address-for-x86_64.patch
        0061-Do-not-try-to-sync-with-Cygwin.patch
        0062-Temporarily-decrement-the-DLL-version.patch)
sha256sums=('SKIP'
            'd4a64a1964ef318c5fe14103ded56040f0d424170cb5bd2d52f5d916e7d168ee'
            '9a2a7ba152a804ea0d077fd43783813cc9cbd1be36d57002d493e66ed45a8604'
            '99f274d59ed1e50a178ef4aef1a27871edf48cfbc30214ff0ae732b1274de08a'
            '663fda4a05385d2bbba9cf8e5ddc3d6569ff256144c373d5c37609b21dfbc0d6'
            'fd75e7902515edeb652323f2d1e80ab7c4ea8529f09815445880809575bbe200'
            '50d8dc042cf6ee8e274047a966ab99537bd476c8dae62c1dc3fba8601a5a9916'
            'b5e62d846a0fd2b144e334936a0bfed8e8c4357b5b6e86bb12a13df9e883a4d0'
            'b5170c868d6a92ce3f9779ea73a033249f29970b09b4c11ef7fd8de83a7d7d43'
            '096216206bee78c609a29b0dcc860c76d61d888e08dcca9d259d82145edde6df'
            'e84f72d56abea15354a6b90dc2bde8dea4d721addc6be13dee83fd973ba07880'
            '9de924ce5f256c483e59026c9c157e8eb0a0160bdd368322a4339f172c966ca3'
            'b78ec73cf5d974a0b7f091ffe41666406e4c5e40186adfad3ef1931e606b9e46'
            '61615b3ca06beb0a7b70450a353dd885d939c6796ce5890198673a9ceab7adb9'
            'da611f93f2ce9fcc5ad8c5397f0acd9e80f93bcc423d96656182c1af752c84db'
            '2350fe4aff9cdcced7d0bc37eb6d2e3a5bcea6f8c554ce622f0ae046868e7a8d'
            'b3fa80660a3048b446f7c27bfc72a2525ed6024674c3b0a0714c3bfe20614e68'
            '88a93c6c04fa37687ad58f2e07be3624061a073ac505daca407413c1c3fb3a56'
            '8534addc5635e2db0003732db8db8866d1d63c4f316e88008579c08e7e00f35c'
            '9a16607fc9b3b164f7cd59e8dd40296d0f261c1dc74d7647cfd314c3a6bca56d'
            'f4dc50a4902a1166f6507fdc1a0fe43fa2a0a61a6423b32e3c71b324f4dbcf10'
            'e052dbba05efa24564f3b840663bc2aa9e5e002401ae6996f9b29bd98d6ea74c'
            'a2a3e58ffb72ad67666beadfb7624d47566597fe2d997029b634ade764064c00'
            'c0f0a45e332ab254e3ea435a9dc4baae61fa70069c2d06d2a6921b1443d28153'
            '8992a3498cc4fdf6fd2bc98069801341dc153f667aaccdce4257c903455724a5'
            'dd0b930e649cb4e547ceabb75c6118828cc3bf30a1f3c2b2b1076003432663a5'
            '69413dabbcad0a3d348b27ef3a9ccaf00989b16c797525df9e4436d59b790841'
            '7ab0c25d57eeaac1b015eddbff805ae7b4603530b24beb601e4476092c204e05'
            '5f23bf090234b7cdaa1bdaa6bc5345fa26b3f09748b25d48e2945641bef10d0a'
            'ecc51238940bdca11e1414ba5a65fef0ead120c3eaed4ce108715bc8fd633038'
            '44d55a7b6441fe8c30abbb4353768ad243806f44a426af3b7161bbe1d943a23a'
            '6af03b473f0723a5ff322595e474901db424cff4d902cfa1fb09e1f4410c1104'
            '23648ff7f780e8762d1684394c45ab44dcf976cb8db265044ad5c36a79c7aefe'
            '9b0e4e250a4b4ea5d8559ba8f65330fc4d4b06915900948a5cb3c16d3138434a'
            'a992eb139d9f2f9217d9462c0fbca3b40b6d9cc944e30c1b3c6d7d970f53ceda'
            'd4647902609f3569ebfbe4df6c745a511538e01eb952d182c41aae7b04864d78'
            'a9ba13e4073f563b282f9b90d81c7a20881cf86f540a663c3bd11bc74db44901'
            'ea8efb8626252098d9663c29c10233c1e53c682b0029df2127c9ba0e8d433358'
            '458c619b21d77038e580e2f744c3a42cda424cff5c16c88bb9971e548684f74e'
            '14c1cf5ad1a759591ff5fad091622403b726e66496082b57500c1b9a6d51c422'
            '08de298c0b5e4729d95be2b47391b9338a5d1aaea26378a3deaacb24b7470cc2'
            '62fdfd0d5400d2df841f3f8b55625e1f394cb65188dcc73229921e7001f49d92'
            'aa3d575e876fcabf22a21e7c25d16d1d566f972a34dc17b8683faf935c45dbf8'
            '7cd6ac9765ee1f672470bf030104aa53925a65e5087dd14565ffe45511bf6dd1'
            '226c907d6a18b80c3b1bb18a0da874c6892308064a0d926575788792e5f9d8df'
            'bd51fef95e711357720260c62785276e9132883a5808eafe6846eebbaf841b64'
            '578320442cd279ef42a67cb23475e0b45e095d8aaffbe9565eb02b8b1f9c4d3d'
            '7fde1a46d1f35d1b14bbfc07293c94e5100f740427ff5e029d0a0fc246253a32'
            '0d71da68d7d516724a08cf5c0a710b1a33684ebf2eb18ddefdc048a4bc1c2435'
            'fef905c9b068c10279a1acdcb52878de28d443f4c66c039cc19eacdad09b0307'
            '748d71924290f698c8993a0134d115545675791df513e92993ea01b5e8bb7978'
            'cf58997940fc3f9b206f036c21cfab83659e0e2f1742b8574ed823e3f9532d93'
            '21117783e6371d342cab398dcb7e51dc2d596faa21957b64b13d3f200adbd5d7'
            'c3ee4c5af9303d3b0962a37328b33bf184b6459636c248c83a734b52b2131432'
            '7870b63d61f9cb355a6f9963230c4e86ccee2574830d149b93f5719e7752dca9'
            'fd931821c317685d0bf23ac7d468ec0d6a282120e926f04e928c75e28225ea1f'
            'cbcaa1e6bccce9137f241e92af0094e0cfb04332898810c6b24dc20c41e4162e'
            'a261cbfd54e4f667a8cef5164607ff8bf984fb1933c12db496a6b8f2d66521a7'
            'b38a90b8943681ae625ae30a6415cccab92a2f2d9bab9b2c9ab4c992d1313912'
            '0991c7539395d942aab7fb5c45900d7b313344a203061133e8826e430eb672e6'
            '48881112377975517813191b69e7ef5cba1f3bfa676614515dbf393fc869980d'
            '089019abe061fc9d5ec93a205873ede268a399d8a1ab539664529cdaddef68ed'
            '04378e9e66a1499173790cd1bf39934b185d82215f642a582f12101c419c6911')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0001-Add-MSYS2-triplet.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0002-Fix-msys-library-name-in-import-libraries.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0003-Rename-dll-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0009-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0010-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0013-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0014-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0015-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0019-strace-quiet-be-really-quiet.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0020-Default-to-disable_pcon.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0021-Introduce-the-enable_pcon-value-for-MSYS.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0026-Disable-the-cygwin-GitHub-workflow.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0027-Do-not-show-Error-dialogs-by-default.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0030-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0031-Cygwin-make-option-for-native-inner-link-handling.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0032-docs-skip-building-texinfo-and-PDF-files.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0033-install-libs-depend-on-the-toollibs.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0034-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0036-Implicitly-support-the-dev-fd-symlink-and-friends.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0037-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0038-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0039-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0040-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0041-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0042-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0043-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0044-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0045-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0046-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0047-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0048-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0049-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0050-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0051-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0052-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0053-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0054-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0055-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0056-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0057-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0058-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0059-Use-MB_CUR_MAX-6-by-default.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0060-Change-the-default-base-address-for-x86_64.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0061-Do-not-try-to-sync-with-Cygwin.patch
  git am --committer-date-is-author-date --ignore-space-change "${srcdir}"/0062-Temporarily-decrement-the-DLL-version.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make

  if test -n "$SIGNTOOL"
  then
    eval "$SIGNTOOL" \
      ${PWD}/${CHOST}/winsup/cygwin/msys0.dll \
      ${PWD}/${CHOST}/winsup/utils/*.exe ${PWD}/${CHOST}/winsup/utils/mingw/*.exe
  fi

  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
