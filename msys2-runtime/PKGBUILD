# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.3
pkgrel=0
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-Fix-incorrect-path-conversion-trailing-slash.patch
        0035-Pass-environment-variables-with-empty-values.patch
        0036-Handle-8-bit-characters-under-LOCALE-C.patch
        0037-Mention-the-extremely-useful-small_printf-function.patch
        0038-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0039-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0040-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0042-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0043-Leave-paths-containing-any-special-characters-alone.patch
        0044-Leave-paths-containing-alone.patch
        0045-Skip-posix-to-windows-conversion-when-is-seen.patch
        0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0047-Arguments-starting-with-are-no-paths.patch
        0048-Prevent-scp-style-arguments-from-being-mangled.patch
        0049-Fixed-path-converting-with-non-ascii-char.patch
        0050-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0053-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0055-POSIX-ify-the-SHELL-variable.patch
        0056-Make-paths-WCS-MBS-conversion-explicit.patch
        0057-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0058-Use-MB_CUR_MAX-6-by-default.patch
        0059-Do-not-try-to-sync-with-Cygwin.patch)
sha256sums=('SKIP'
            '59c53c74c5c0e6f8d0020eb2d4b7ea69e29f0682b985185fac07c3a90966c88b'
            '97240278790878f2ca238c64c025eab710281b765a71009c619b663ca120ffad'
            '00156f6d69b3d0861c6a71f4543e781b934075847896d630779b76ac1db42133'
            'b7a1a2cde65b7e37e37021c7e871ca4d6a466ad7a0d7ff38263cc9784d13915e'
            '3cfbfa5a2018e388aa5e5b55af122712f44b548d7f9a833abb79550b92448bed'
            '930c0dff9737879a2756d1a3b0db92cfdbaf614975b18642ce4cc0fcf8623265'
            '3b39efc147820cabacc502916670556c3473280f7c09563e00d690bdd64a0fdc'
            '28032e262ca8d18cbc3525638b0fe3faf303221152be8531feffe30505c2568d'
            '83f633bad763ee174b76e2bd4ccfa14e892300246afe69405657b720401013d4'
            '2bfad7ce6c041fdf99654699355ee67791d7241309b238d0f88950542e36c40d'
            '2136d6edaabfef2ce43e6b70f2f567efc3e235cd1ea64bc31c5d436d3a14d3c2'
            '3f7d90d1ba8627ac45b7fb303dbcc1bcc12f3f34368307a6f41a2e9e972813e1'
            '435142cf6f5e0378fea23d5ed892014444665eab452055376d27994dfe59b2c2'
            '0e55c44dda20f640f78fc55ec08fa6b5cae6990c79f153d48d2600f4d70d6e2a'
            'ac81d145867e62d13e07449ab29657f9933076639a414670d7b0c7bad029dfea'
            '51f611397d149548d3027a6fc54ee5829425f22a7587f0f8e243d210508c5c84'
            'f769d52027f645754af7db2062c0bfcb2006735b03f30c132aded61b2d60106d'
            '305adee33cbdf8d6f4aa65d4e2c58040df73bca1a1e47812ba5f5dfa2cfcd601'
            '36522faaa07d249d8c8622ae93ff4f3de8250596b98b2b3805828df93ff1a4b8'
            '0256450246a62682ccf33c28ff4baa26097a24122999e4c079cb10929b82dbe0'
            '3400760c66112c1c3ea434973f131e1d192433479efec994dca20f28ea518682'
            'c191802c7ac6c3fcd2fe935dd1363ce42369dc933c4685749d96045bac00496d'
            '6decbbdf1b098e1724b09a72e0e6173f46b38fe50d33a21623f7bb7b3c28e35c'
            '994ddc7869d36a9a6fb1ffa6e36a7dca059103a4cec06d57f99c1041e2d4db78'
            '40c00d6415c270c8075de7beec63d9cdb4e7ae74a4ff482d35bdc9f357a4224a'
            'ca7122c6d994df98620e83dedcbaaaa075ee581328d19c83e387127a47dce40c'
            'b61f88a4a969deefa4c056a865d6716e1d2f5f03ccd59d7bde741f74feefc1bb'
            '160c5d33062aae3b1c3f3e3aa39c1852069fee98e9d2e89d56e9a90eea2e41a6'
            'd8e014c2e458ed63439430778f9839f7f122d1fca920d6d15de38b908eed7344'
            '4ceb90aefeae03b3461c141300fa0725c016bd76f07219df73dd506158db5dc1'
            'decc6c00baee89313b102f6042ca5d82474097ca53232d899c8fb485de5cb91d'
            'dbbf2159b68efde6120715fcc61d0821510f7b36f6d9cc3b2603c2909d6a8334'
            'cb257cc704c53359df61c2735e07ea8d539a757379ff62d33b6513dcbc2ecac5'
            '2a0a9b10a4fc89c257a55893efd82b0b4f01422d50a20079a3e034f9b2ccf93c'
            'd02c97f3b09583ab2cf9f9ff92f09d52ef1c6d83d3704720ff7605f8b8491d41'
            '1421df6b0f55c94b485b03297838769ec55662864aa20acf850411c6bd021c4a'
            'ae573a68eb4940899ace68eede677c71d09b85ff2c8ca842c25421b2146d52c3'
            'f0181c9f505d452b8dac9b79b2a9bb5d24457860e37848a93b7cedd6342e9163'
            '08cebbd4d8338ac07d51561d628b5234d555836471ac37f7f05a4f6d5a0ea814'
            '950e277a1f866fe394eaaf7d3fa43034e9e1ace00e448f0b3ea98fe3c25f0dff'
            '9e01ee6068bc0269a3e69d4ff1a3e716f8213a32c6be45e311b4edcaa4dd8f14'
            '0a4b9619f79cdc49f06270a2f513a50f4fa91f3fcf1c356789a688894892d0a4'
            '65a8a42b9d9ca047b5deb4837234e2ca725575e7f040ee90a4916f12872e0a3f'
            '45c273f1b6ca8f65ea430545e28943e65b0e989f17cfce023425d381aa0fd8af'
            'd190b1c4c88f92589459b03b669ebbba0d2c4b7147bd59cea7bc22958d546846'
            '6ba44f14cb5f47acb1f0901eac911e23729147a032e596a99b82437590bf2026'
            '02be525bb570188a99d5810a3c65823101956b146ccc5529c6f04c668685ec8d'
            '6535b63fcaa54aaeadd65fccc3e8ed538bc5a1d2738000588ab43c2e5ed1afe0'
            'e62da1b4886fc7dfc28d99f95781ff0a243efa63b5eaba7bec552c9b54420368'
            '6e5bb4f036a50260c3498e9bc008420addc3128b1a1f5ccaac434695fb575900'
            'f17fb682c4959a390fd9c3128cf313c8554ee1c0debfe045c80faf081cf9b509'
            '208cabffb1a459ca5fdfe14501d87df79fc5b84ee4ee01535129625c8da6bcf9'
            'ef65eb1f09e545f39a78d22e38ca86050351f55fb485422bbfc031721cc77517'
            'd2fd4b204d8da9be70213018201d24972a37cafaf9219121ba43d5e03d538966'
            'b97b6c62127e7ccd0ee46a8143c1a06df780ac85a4edf6ce72eb3b51de4d8c97'
            '84380e1ebe5e8659e258a45388eb6c9b0a31f87aefac6dcc1c385cda5ad11619'
            '693610fbe321a26f7b2f9dc1db41fb90f748f383b1f7922e4a43cededc39e0aa'
            'fafd2acb54add9f7e30487d68b949cf5b00f14a431dfb7408a329d6bc988b992'
            'ac3ff8bcb2202f7c5af9f7a3494ca1c511f8513bbfd2f5a60bad0b5329a26b58')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  if test false != "$(git config core.autocrlf)"
  then
    git config core.autocrlf false &&
    rm "$(git rev-parse --git-path index)" &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS2-triplet.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Fix-msys-library-name-in-import-libraries.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Rename-dll-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace-quiet-be-really-quiet.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Default-to-disable_pcon.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Introduce-the-enable_pcon-value-for-MSYS.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Disable-the-cygwin-GitHub-workflow.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Do-not-show-Error-dialogs-by-default.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Cygwin-make-option-for-native-inner-link-handling.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-docs-skip-building-texinfo-and-PDF-files.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-install-libs-depend-on-the-toollibs.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0052-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0053-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0054-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0055-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0056-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0057-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0058-Use-MB_CUR_MAX-6-by-default.patch
  git am --committer-date-is-author-date "${srcdir}"/0059-Do-not-try-to-sync-with-Cygwin.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
