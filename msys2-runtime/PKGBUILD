# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.1.6
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Rename-dll-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-Special-case-for-converting-root-directory-to-have-t.patch
        0010-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0011-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0012-strace.cc-Don-t-set-MSYS-noglob.patch
        0013-Add-debugging-for-build_argv.patch
        0014-Add-debugging-for-strace-make_command_line.patch
        0015-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0016-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0017-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0018-fixup-Do-not-create-cygwin-symlinks.-Instead-use-dee.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-Don-t-create-WSL-symlinks-for-MSYS2.patch
        0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0024-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0025-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0026-recursiveCopy-Always-close-the-used-handle-with-Find.patch
        0027-fixup-Add-functionality-for-converting-UNIX-paths-in.patch
        0028-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0029-Fix-incorrect-path-conversion-trailing-slash.patch
        0030-Pass-environment-variables-with-empty-values.patch
        0031-Handle-8-bit-characters-under-LOCALE-C.patch
        0032-Mention-the-extremely-useful-small_printf-function.patch
        0033-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0034-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0035-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0036-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0037-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0038-Leave-paths-containing-any-special-characters-alone.patch
        0039-Leave-paths-containing-alone.patch
        0040-Skip-posix-to-windows-conversion-when-is-seen.patch
        0041-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0042-Arguments-starting-with-are-no-paths.patch
        0043-Prevent-scp-style-arguments-from-being-mangled.patch
        0044-Fixed-path-converting-with-non-ascii-char.patch
        0045-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0046-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0047-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0048-kill-kill-Win32-processes-more-gently.patch
        0049-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0050-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0051-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0052-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0053-POSIX-ify-the-SHELL-variable.patch
        0054-Make-paths-WCS-MBS-conversion-explicit.patch
        0055-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0056-Use-MB_CUR_MAX-6-by-default.patch
        0057-Mirror-master-and-main.patch)
sha256sums=('SKIP'
            '7cb9d54bb20e68ae4258dea996a716ded850d3b36536ba60f7f2ae248f720762'
            'da012643859862eafb8b6375c218f6c5f2fb064948885b91868049706a0b61ce'
            '46692a310e7abe74baed1d572da8ad89861ab301eb33b885430a21d929a430fb'
            '9ab0e41ddef87ae04df317c15fa223951d09c19d7deb617cfd9ac04dbe9ce2ec'
            '7ed041118fba3306f7c682450ec37d466f35165b22922e0ba4d6817e36748502'
            '2f6c0f44e2aa50152081994a5b7b8329bf61a89461bcd97e2e96e4c402a9b429'
            '2cad0d764889b9a91c376a7a2bab541194b78e4f911eee14a1f43fb01d75a2e5'
            '03e907d91beeab4b1d2c02fc8b8e6c1cefbd1965bf6a925b24bfbb8a00526e76'
            '0e36fee2a4b41d33fef2c008f82ae3a1155b0f262756181d1b2cab56401ee869'
            '4e735c54fab24f1676c69c2efb8db3c7686fc586fb35abd4262f0aa5a84cfb62'
            '58472f1ffb94b53d04fd0df57117e6da5bcb5bbdd95d81fde6c2107229b1ba2f'
            '811cc2361c555317b4e90a808ac4f7dc40cf565999e5f44bfbae88cbf80eac8e'
            'b2f95d64b0bf24e768c8ed68bd9d9b2de794e605f1e5eadcd67c9777b7c8a44f'
            '0077fff931146baa3ad19f5cb7c59482149865661e17c1eeff1c802f4468a7eb'
            '90b7eedb2c96fd3337e28dd46b8837f8aea7c5fd9e9ffd057b09c5028e57f70e'
            '4850b69370aa95fd0ac885da83a12862cd3cf0a2ae7f401d5ab6450fbe119db6'
            '87a807a3c76a22586c643211dc335f5c5955e58aebca67783a88839dd842005d'
            '9e33f7649df0ce1990b56a34015b9f70b0921e5fea44fa5b6181e2394ee5d0a8'
            '92c68f4c9355217190eafea404e9bafd23cdfe38a91ab516359dad58f76a9d31'
            '7d5d08768c18db40912541652b4869b068793f717901f1803eb84403d75c4094'
            '2f11ba663bbd4e2e47009447c70abee47369cdd5ecd37d33c2d145a070d5ff97'
            '376e6720d20f835344bc6b29ec952ef22b2e76ca97742db6167af17fe5f82fec'
            '983f5bec51ba2d9627ae32fc81086c08570227aa5c80acfd4b72b42bad99594b'
            '1e4552bab2f3b852171fb32c7f5ba771e96dffdebbb0a58f4c8f0cdef03f1c27'
            '371d990bca6034381cf97591912f0abb5bd4fb8a37543cec8432311a23f7c32f'
            '42b2413b846d315f70ea631f0bacc806d8220345c8b4f305e6b48a6e60433ec7'
            '30df3342e6504f0f370dc59ad53aabeda2e037d5eff5c54da829c32495cb544e'
            '4184954d737e2678c919790d0f70153b13ebe0d797ca15b9b7d382eac872f2c4'
            'bb3b552e870021215c7a70b00eeb3aa14f93a08459ea35eb30bc1b0df4a5bb86'
            '80853181d7b878dc4dc79a4640c69cf92cf10acd925a337a3ddff4f79259e8c3'
            '7d3c1a7a893c19d3d5cfec5a9d8c189783915ca998bb1c749508f0f87e7c50f6'
            '4fb7490e6260d618fe0a67c926395447b28f823c4c3d16c446995f264959fed9'
            'a7439dfae9cb525e6dcaebb2f8d84b2a655b367e81dc77664d2cc2abcaf58c9c'
            'b089cfaa2e3ce622967f130ffbdcd610b4144d3409b1e0dea535823e3ec71d97'
            'a7a5e0ba7fc023559a8012e52a21531c3eb140bbd665e7df3696c37ed1c64cad'
            'cfe60feb34945533c0c0815829d3ed07010cda750f077031ae424c479ab491f9'
            '4d9843a1e416600dee586e12c62c4c2aff32669333aadae760301669dc1821f6'
            '2135124d784807905b044b2f23c308c5db80844cdafd2215543a0d7436a39008'
            '669b96017cd5c29887277717bbb42a339c88c2b858ec835ec83e374015c84c7f'
            'a760f402f19505933ea9ecddfd47a2485f31053ecd75b3f923db33c810bc393c'
            '30ef2bbaa5292e4574862c4849be218fcb8199ba7e4a86667a694f548938edd7'
            '076be8a7e8891c29d1ca61af52488be70ea999736ca44fe0fae1430bf96d12fb'
            '15a5fdf098d41ff6ac9196f75c1fa43200f2d6d440a1fde0b2feb5135f893f38'
            'fe465387cb24d0fafb2ff5ed929e67b49e5d2f31cf27c9c9944cf75b5383a8f8'
            'fab47db61548d890c5ab041a18e2fc7a07c28697639c28bd882d0033256806aa'
            'b50145789c4a7ab18cf60f8b9310ed3dfbe03e7dcaafa31bf2af46404cfdeee7'
            'b1de76212166018f32e75fec77edf8d049c451bd28127b3773d6213d61d9d390'
            '59e543cd285598630397ef499c9225a52d57023c13841bc839c35e8da67ec015'
            '35d2613cf3f644f39d63fd328ab32315698c7ef3b57e6e70159998f3887d776d'
            '2c7015505ea461249f5433a5c4e47b94813eab834332b0721a8b20afa7e6118e'
            '75b7a44f1358569b08b0ee78ff6dfdfaa309dddbe07eb5289c9a314318b451ff'
            '8727ba9a96b74ef5cfb931282f088504840cb601975deb553b73e8af33ad197c'
            '19f67745b886b0e16b868bc68302263962093cf72b856a6fae87228b7f29bf9d'
            '696c436789c4acee3698d350d8ff4db1efe2255c6424e587d22563f762bd07e0'
            'd4a9b541b9e3c30c0a85a0fd7dcb9a6282449137ed05924df13e984bc4658c8e'
            '507fe15bbb710e1e0f3280c47c381123c40920f09ded43754be7167ed4fd8683'
            'c82a64ae0c3883c41cbd153256c4f8c5b4e071d11e61639ad896eb73deedb7d5')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS2-triplet.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-dll-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-fixup-Do-not-create-cygwin-symlinks.-Instead-use-dee.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-strace-quiet-be-really-quiet.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Default-to-disable_pcon.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Introduce-the-enable_pcon-value-for-MSYS.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-Don-t-create-WSL-symlinks-for-MSYS2.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-popen-call-usr-bin-sh-instead-of-bin-sh.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-CI-add-a-GHA-for-doing-a-basic-build-test.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-recursiveCopy-Always-close-the-used-handle-with-Find.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-fixup-Add-functionality-for-converting-UNIX-paths-in.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Expose-full-command-lines-to-other-Win32-processes-b.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0052-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0053-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0054-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0055-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0056-Use-MB_CUR_MAX-6-by-default.patch
  git am --committer-date-is-author-date "${srcdir}"/0057-Mirror-master-and-main.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
