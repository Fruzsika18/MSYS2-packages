# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.1.5
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
groups=('base')
makedepends=('cocom'
             'texinfo'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'diffutils')
# options=('debug' '!strip')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Rename-dll-from-cygwin-to-msys.patch
        0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0005-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
        0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0008-Do-not-convert-environment-for-strace.patch
        0009-Special-case-for-converting-root-directory-to-have-t.patch
        0010-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0011-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0012-strace.cc-Don-t-set-MSYS-noglob.patch
        0013-Add-debugging-for-build_argv.patch
        0014-Add-debugging-for-strace-make_command_line.patch
        0015-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0016-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0017-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Default-to-disable_pcon.patch
        0020-Introduce-the-enable_pcon-value-for-MSYS.patch
        0021-Don-t-create-WSL-symlinks-for-MSYS2.patch
        0022-Fix-incorrect-path-conversion-trailing-slash.patch
        0023-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
        0024-Pass-environment-variables-with-empty-values.patch
        0025-Handle-8-bit-characters-under-LOCALE-C.patch
        0026-Mention-the-extremely-useful-small_printf-function.patch
        0027-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
        0028-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
        0029-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
        0030-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
        0031-Leave-Git-s-name-and-message-arguments-alone-please.patch
        0032-Leave-paths-containing-any-special-characters-alone.patch
        0033-Leave-paths-containing-alone.patch
        0034-Skip-posix-to-windows-conversion-when-is-seen.patch
        0035-Also-leave-Git-s-rev-.-name-syntax-alone.patch
        0036-Arguments-starting-with-are-no-paths.patch
        0037-Prevent-scp-style-arguments-from-being-mangled.patch
        0038-Fixed-path-converting-with-non-ascii-char.patch
        0039-path-conversion-Introduce-ability-to-switch-off-conv.patch
        0040-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0042-kill-kill-Win32-processes-more-gently.patch
        0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
        0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
        0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
        0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
        0047-POSIX-ify-the-SHELL-variable.patch
        0048-Make-paths-WCS-MBS-conversion-explicit.patch
        0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0050-Use-MB_CUR_MAX-6-by-default.patch
        0051-popen-call-usr-bin-sh-instead-of-bin-sh.patch)
sha256sums=('SKIP'
            'dd1ba61024998e2afc22df0c672a47ad2f497a3e473edddd7cb0dddbed5bb74c'
            '61e7a49d57f9ba8be7cf2df938b5495eb50171932fe2638a738e58264f6ec4cd'
            'f6eb49dc48cc38bbd32e7f8deb41f3437089a89a2238870c4f706aac0a7140d2'
            '54a8375c24d71bf762736c5bf011fb17bad7365f8d14a36e63d0c48c6b89117f'
            '11a9074954f43f5864cb98f5f8ae6da3b8b7cf672ab76fd07a737a98b42a8dea'
            '590592f8989f3b129ce79c992f75a5539548bdcf05a3ef756bc99497a308678c'
            'cf5248ee42024ec8fa9c7dc4ff108a1887ee9ed38506ebe6f8861b36ec421485'
            '4bd41973baaf054685988f7049ce0964e76bbfe1c9c09dcbe8a8a0cd105b5f9e'
            '7eb35c1609caa572853975e296ec395b034b2bb0e287fa007e249fa213f3f784'
            '403d37aedd13d505a7faff54ebf269425ac6f30e7c457c0906b1e3f820328abd'
            'bc5531901e6d22819c672e5dda30bdf52911ae68156b64e54406e9ebb8a16634'
            '7de14755083e8976b2909a7b79d7a26e800599c6155499ea927723550e0473fb'
            'a598287f4e576d3b7d09994600e37c07f3659bdc884e1b7397558397d6fcbf6f'
            '3ee9b06d2ef0413e3dddd967a24c2356de10b8531975c2a7e734d857495edacf'
            '234693fe5cf7ea975a6328a554494e19a593a2235ebda01d674525c234e43357'
            '16e0cdc13dec3bec16e0d13e0736f3e562e9fcb5b9422333a332f613f1fba240'
            '977b8d16c6705e6013fa796722515790f43bcdf5c0be14b7fb3d1ffebd9e12f7'
            'e1eecd89ba4c3140c38ae95189e1e2aab162b73fcededbcc74703ba765bf9a76'
            '7e103f9087e6b9a4bd968708c23e7cba65048540381e0099dca98a5f4974b81f'
            'e059d8be4f4277e2e6f17b3feeb897b586731cb6d0abd85fe794ace921a92cc1'
            'a958cd51946bfaea7e3401e0b078cf6ac62b2f07879a3b050062f6f914d5fd32'
            '59d2670669b421e683dd7790afd5bf30f0abad96c867c9e0ea19052498607403'
            '104885a6e507b2850278e93eccb251955d40b99de82a572a6224f7455bdc730b'
            'f4fecb08621ec1f8be504a0f09ef09b6c54763a1ac385c2f96480bf79f87333c'
            'fcd7d81e0ee8545233642ad7d605fe8e09b3849de4cd78eb75fbf5692175e41e'
            '6456a0ab402589e9f7c685ebb1cd29db02e63d45e6bf4f1013d46cc53d7060b7'
            'f06fb9a97f505030af068aaa8de71e175152aba17aa086f0ea13660bd8085edb'
            '68e280d0f8a9f2004e14923364ff51358ad68a7753f23d87a6efc06f5e3e2e10'
            'c7e0290b83a9b6f4d6e5cb6023c27f5e035908b5d924aa031d7786025d8720ef'
            '4490f46437bcaf4842c00304d71a004ae1803b5320a37f369a93bfbf69ecaa00'
            '1efff6b2220231a69bebbf73d287e82d02686634b4a905d004f2ed13728a9fa5'
            'cee0f8b92e7a3dd79b0d91dc9e5d841bde2240e5a785a6aad72c0335fc635016'
            'ac4b91aab3d2df06f36ea2cfa4fdddde121f526f3ad3ba7c994c2ae0ad12f732'
            '07bbfeb2fdde585d5d3661c29cb6b41b4ebd92cea6a4f0ffee67d10f5fd02ff9'
            '3b1a77555f8564ec5aae7b367c610909b8ffe779142d88c3e98b3cbf57d327c8'
            '4df2022457077c5fc9492325c379d8ab6b0b0315b22b4f28505cb7566b199281'
            '2eb88239d191a435a2bf59acbcf0174cbd0954f8c7e416206410b4ae7ad99be3'
            '24cd54b443f7477ebaa0e9ce127937de96465a144cfcc1c4646e609a47293e43'
            '361405eec146df0c0192945e9eb30f84eba6e5d3498cc81746f25c73cc335d3e'
            '59a6196b3517a1992f0589c487966729f3e518678c7278a2322fc37ffcd5046b'
            '13b54ac22e070840c3014dd3271f1f4bde65061f7be219a6f722c4d0d8769478'
            'eec4920208e406558874d7eaf8483d140dffae186675911ba18e72fd37c86822'
            '10b88aa446033f4665035ccdc42fa17e3a9bf157bd93d61acca3e9a37a628e7a'
            '62767fe6f20333638cfe934263a434b5bfa9fb49248371d7ed8ed2523642ac62'
            '61fcc0a37d87792846301c8e4fd8bdef2374b9726acfad9654b6cdf20aa0b9ea'
            '7e10c14e7a68a075765035a2a492e5eebd3fa89133d33d41bcc83bbebc36bb63'
            'c20d49d739d13b3c4fbfcf18a7848d60e713055f96c9addc089b123edf1e6266'
            'ac989679dfdf1083b0ee6b0fa23cd62b17728f8b3ce6e2170de7e4b0058c3ee6'
            'c44b7b7512cca60bc45a331b5676f94bb7ab0d4b011032c353be09f49bb07220'
            '4a5986d828a7eef247f31016e47ac176053e839ef8aaa0d86b330bae078bcb15'
            '28a84b0d10f91e35869fe52a741f9e50a663997aef4427621b4d7043653b404c')

# Helper macro to help make tasks easier #
del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
  git am --committer-date-is-author-date "${srcdir}"/0001-Add-MSYS2-triplet.patch
  git am --committer-date-is-author-date "${srcdir}"/0002-Rename-dll-from-cygwin-to-msys.patch
  git am --committer-date-is-author-date "${srcdir}"/0003-Add-functionality-for-converting-UNIX-paths-in-argum.patch
  git am --committer-date-is-author-date "${srcdir}"/0004-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
  git am --committer-date-is-author-date "${srcdir}"/0005-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
  git am --committer-date-is-author-date "${srcdir}"/0006-Do-not-create-cygwin-symlinks.-Instead-use-deep-copy.patch
  git am --committer-date-is-author-date "${srcdir}"/0007-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
  git am --committer-date-is-author-date "${srcdir}"/0008-Do-not-convert-environment-for-strace.patch
  git am --committer-date-is-author-date "${srcdir}"/0009-Special-case-for-converting-root-directory-to-have-t.patch
  git am --committer-date-is-author-date "${srcdir}"/0010-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
  git am --committer-date-is-author-date "${srcdir}"/0011-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
  git am --committer-date-is-author-date "${srcdir}"/0012-strace.cc-Don-t-set-MSYS-noglob.patch
  git am --committer-date-is-author-date "${srcdir}"/0013-Add-debugging-for-build_argv.patch
  git am --committer-date-is-author-date "${srcdir}"/0014-Add-debugging-for-strace-make_command_line.patch
  git am --committer-date-is-author-date "${srcdir}"/0015-environ.cc-New-facility-environment-variable-MSYS2_E.patch
  git am --committer-date-is-author-date "${srcdir}"/0016-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
  git am --committer-date-is-author-date "${srcdir}"/0017-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
  git am --committer-date-is-author-date "${srcdir}"/0018-strace-quiet-be-really-quiet.patch
  git am --committer-date-is-author-date "${srcdir}"/0019-Default-to-disable_pcon.patch
  git am --committer-date-is-author-date "${srcdir}"/0020-Introduce-the-enable_pcon-value-for-MSYS.patch
  git am --committer-date-is-author-date "${srcdir}"/0021-Don-t-create-WSL-symlinks-for-MSYS2.patch
  git am --committer-date-is-author-date "${srcdir}"/0022-Fix-incorrect-path-conversion-trailing-slash.patch
  git am --committer-date-is-author-date "${srcdir}"/0023-Pass-the-TZ-variable-to-non-msys-programs-if-it-is-s.patch
  git am --committer-date-is-author-date "${srcdir}"/0024-Pass-environment-variables-with-empty-values.patch
  git am --committer-date-is-author-date "${srcdir}"/0025-Handle-8-bit-characters-under-LOCALE-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0026-Mention-the-extremely-useful-small_printf-function.patch
  git am --committer-date-is-author-date "${srcdir}"/0027-Add-a-helpful-debug-message-for-posix-to-windows-con.patch
  git am --committer-date-is-author-date "${srcdir}"/0028-Stop-assuming-that-there-are-no-spaces-in-POSIX-styl.patch
  git am --committer-date-is-author-date "${srcdir}"/0029-Avoid-unnecessary-recursion-in-find_path_start_and_t.patch
  git am --committer-date-is-author-date "${srcdir}"/0030-Leave-arguments-starting-with-a-tilde-or-quote-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0031-Leave-Git-s-name-and-message-arguments-alone-please.patch
  git am --committer-date-is-author-date "${srcdir}"/0032-Leave-paths-containing-any-special-characters-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0033-Leave-paths-containing-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0034-Skip-posix-to-windows-conversion-when-is-seen.patch
  git am --committer-date-is-author-date "${srcdir}"/0035-Also-leave-Git-s-rev-.-name-syntax-alone.patch
  git am --committer-date-is-author-date "${srcdir}"/0036-Arguments-starting-with-are-no-paths.patch
  git am --committer-date-is-author-date "${srcdir}"/0037-Prevent-scp-style-arguments-from-being-mangled.patch
  git am --committer-date-is-author-date "${srcdir}"/0038-Fixed-path-converting-with-non-ascii-char.patch
  git am --committer-date-is-author-date "${srcdir}"/0039-path-conversion-Introduce-ability-to-switch-off-conv.patch
  git am --committer-date-is-author-date "${srcdir}"/0040-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
  git am --committer-date-is-author-date "${srcdir}"/0041-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
  git am --committer-date-is-author-date "${srcdir}"/0042-kill-kill-Win32-processes-more-gently.patch
  git am --committer-date-is-author-date "${srcdir}"/0043-Allow-overriding-the-home-directory-via-the-HOME-var.patch
  git am --committer-date-is-author-date "${srcdir}"/0044-Respect-db_home-setting-even-for-the-SYSTEM-account.patch
  git am --committer-date-is-author-date "${srcdir}"/0045-Respect-the-db_home-env-setting-under-more-circumsta.patch
  git am --committer-date-is-author-date "${srcdir}"/0046-Allow-native-symlinks-to-non-existing-targets-in-nat.patch
  git am --committer-date-is-author-date "${srcdir}"/0047-POSIX-ify-the-SHELL-variable.patch
  git am --committer-date-is-author-date "${srcdir}"/0048-Make-paths-WCS-MBS-conversion-explicit.patch
  git am --committer-date-is-author-date "${srcdir}"/0049-Handle-ORIGINAL_PATH-just-like-PATH.patch
  git am --committer-date-is-author-date "${srcdir}"/0050-Use-MB_CUR_MAX-6-by-default.patch
  git am --committer-date-is-author-date "${srcdir}"/0051-popen-call-usr-bin-sh-instead-of-bin-sh.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('msys2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
